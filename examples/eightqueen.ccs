(VAR 
  n x y z w 
  n1 n2 n3 n4 n5 n6 n7 n8 
  q qs y1 ys 
  xdist ydist head tail permList permTail item list first list1
)
(SIG
  (eightqueen 8 1)
  (perm 2 1)
  (del 2 1)
  (safe 1 1)
  (noattack 3 1)
  (diff 2 1)
  (neq 2 1)
)
(SORT
    (oo                 -> unum)
    (s                  unum -> unum)
    (Nil               -> List)
    (c                  unum List -> List)
    (empty              -> empty)
    (eightqueen         unum unum unum unum unum unum unum unum -> empty)
    (perm               List List -> empty)
    (del                unum List -> List)
    (safe               List -> empty)
    (noattack           unum List unum -> empty)
    (diff               unum unum -> unum)
    (neq                unum unum -> empty)

)
(RULES 
  eightqueen(n1,n2,n3,n4,n5,n6,n7,n8) -> <empty> <=
  	perm(c(n1,c(n2,c(n3,c(n4,c(n5,c(n6,c(n7,c(n8,Nil)))))))), c(s(oo),c(s(s(oo)),c(s(s(s(oo))),c(s(s(s(s(oo)))), c(s(s(s(s(s(oo))))),c(s(s(s(s(s(s(oo)))))), c(s(s(s(s(s(s(s(oo))))))),c(s(s(s(s(s(s(s(s(oo)))))))), Nil))))))))) -> <empty> ^
  	safe(c(n1,c(n2,c(n3,c(n4,c(n5,c(n6,c(n7,c(n8,Nil))))))))) -> <empty>
  		
  perm(Nil,Nil) -> <empty>
  perm(c(head,tail),permList) -> <empty> <=
    del(head,permList) -> <permTail> ^ 
    perm(tail,permTail) -> <empty>
 
  del(item,c(item,list)) -> <list>
  del(item,c(first,list)) -> <c(first,list1)> <=
    del(item,list) -> <list1>
 
  safe(Nil) -> <empty>
  safe(c(q,qs)) -> <empty> <=
    safe(qs) -> <empty> ^ 
    noattack(q,qs,s(oo)) -> <empty>
	
  noattack(y,Nil,x) -> <empty>
  noattack(y,c(y1,ys),xdist) -> <empty> <= 
  	diff(y,y1) -> <ydist>  ^
  	neq(ydist,xdist) -> <empty> ^
  	noattack(y,ys,s(xdist)) -> <empty> 
  
  diff(s(x),s(y)) -> <z> <=
    diff(x,y) -> <z>
  diff(oo,s(y)) -> <s(y)>
  diff(s(x),oo) -> <s(x)>

  neq(s(x),s(y)) -> <empty> <= neq(x,y) -> <empty>
  neq(oo,s(y)) -> <empty>
  neq(s(x),oo) -> <empty>
  
    
)
(COMMENT
    original author; Maja H. Kirkeby

    safe (c(s(oo), c(s(s(s(s(s(s(s(oo))))))), c(s(s(s(s(s(oo))))), c(s(s(s(s(s(s(s(s(oo)))))))), c(s(s(oo)), c(s(s(s(s(oo)))), c(s(s(s(s(s(s(oo)))))), c(s(s(s(oo))), Nil)))))))))

    perm (c(s(s(s(s(s(s(s(oo))))))), c(s(oo), c(s(s(s(s(s(oo))))), c(s(s(s(s(s(s(s(s(oo)))))))), c(s(s(oo)), c(s(s(s(s(oo)))), c(s(s(s(s(s(s(oo)))))), c(s(s(s(oo))), Nil)))))))))  (c(s(oo), c(s(s(s(s(s(s(s(oo))))))), c(s(s(s(s(s(oo))))), c(s(s(s(s(s(s(s(s(oo)))))))), c(s(s(oo)), c(s(s(s(s(oo)))), c(s(s(s(s(s(s(oo)))))), c(s(s(s(oo))), Nil)))))))))

    eightqueen (s(oo)) (s(s(s(s(s(s(s(oo)))))))) (s(s(s(s(s(oo)))))) (s(s(s(s(s(s(s(s(oo))))))))) (s(s(oo))) (s(s(s(s(oo))))) (s(s(s(s(s(s(oo))))))) (s(s(s(oo))))
)