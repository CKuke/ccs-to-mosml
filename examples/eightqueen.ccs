(VAR 
  n x y z w 
  n1 n2 n3 n4 n5 n6 n7 n8 
  q qs y1 ys 
  xdist ydist head tail permList permTail item list first list1
)
(SIG
  (eightqueen 8 1)
  (perm 2 1)
  (del 2 1)
  (safe 1 1)
  (noattack 3 1)
  (diff 2 1)
  (neq 2 1)
)
(SORT
    (ze                 -> unum)
    (s                  unum -> unum)
    (null               -> List)
    (c                  unum List -> List)
    (empty              -> empty)
    (eightqueen         unum unum unum unum unum unum unum unum -> empty)
    (perm               List List -> empty)
    (del                unum List -> List)
    (safe               List -> empty)
    (noattack           unum List List -> empty)
    (diff               unum unum -> empty)
    (neq                unum unum -> empty)

)
(RULES 
  eightqueen(n1,n2,n3,n4,n5,n6,n7,n8) -> <empty> <=
  	perm(c(n1,c(n2,c(n3,c(n4,c(n5,c(n6,c(n7,c(n8,null)))))))), c(s(ze),c(s(s(ze)),c(s(s(s(ze))),c(s(s(s(s(ze)))), c(s(s(s(s(s(ze))))),c(s(s(s(s(s(s(ze)))))), c(s(s(s(s(s(s(s(ze))))))),c(s(s(s(s(s(s(s(s(ze)))))))), null))))))))) -> <empty> ^
  	safe(c(n1,c(n2,c(n3,c(n4,c(n5,c(n6,c(n7,c(n8,null))))))))) -> <empty>
  		
  perm(null,null) -> <empty>
  perm(c(head,tail),permList) -> <empty> <=
    del(head,permList) -> <permTail> ^ 
    perm(tail,permTail) -> <empty>
 
  del(item,c(item,list)) -> <list>
  del(item,c(first,list)) -> <c(first,list1)> <=
    del(item,list) -> <list1>
 
  safe(null) -> <empty>
  safe(c(q,qs)) -> <empty> <=
    safe(qs) -> <empty> ^ 
    noattack(q,qs,s(ze)) -> <empty>
	
  noattack(y,null,x) -> <empty>
  noattack(y,c(y1,ys),xdist) -> <empty> <= 
  	diff(y,y1) -> <ydist>  ^
  	neq(ydist,xdist) -> <empty> ^
  	noattack(y,ys,s(xdist)) -> <empty> 
  
  diff(s(x),s(y)) -> <z> <=
    diff(x,y) -> <z>
  diff(ze,s(y)) -> <s(y)>
  diff(s(x),ze) -> <s(x)>

  neq(s(x),s(y)) -> <empty> <= neq(x,y) -> <empty>
  neq(ze,s(y)) -> <empty>
  neq(s(x),ze) -> <empty>
  
    
)
(COMMENT

    safe(c(s(ze), c(s(s(s(s(s(s(s(ze))))))), c(s(s(s(s(s(ze))))), c(s(s(s(s(s(s(s(s(ze)))))))), c(s(s(ze)), c(s(s(s(s(ze)))), c(s(s(s(s(s(s(ze)))))), c(s(s(s(ze))), null)))))))))

    perm (c(s(s(s(s(s(s(s(ze))))))), c(s(ze), c(s(s(s(s(s(ze))))), c(s(s(s(s(s(s(s(s(ze)))))))), c(s(s(ze)), c(s(s(s(s(ze)))), c(s(s(s(s(s(s(ze)))))), c(s(s(s(ze))), null)))))))))  (c(s(ze), c(s(s(s(s(s(s(s(ze))))))), c(s(s(s(s(s(ze))))), c(s(s(s(s(s(s(s(s(ze)))))))), c(s(s(ze)), c(s(s(s(s(ze)))), c(s(s(s(s(s(s(ze)))))), c(s(s(s(ze))), null)))))))))

    eightqueen (s(ze)) (s(s(s(s(s(s(s(ze)))))))) (s(s(s(s(s(ze)))))) (s(s(s(s(s(s(s(s(ze))))))))) (s(s(ze))) (s(s(s(s(ze))))) (s(s(s(s(s(s(ze))))))) (s(s(s(ze))))
)